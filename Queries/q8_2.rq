PREFIX : <http://www.oxfordsemantic.tech/f1demo/>

# Drivers and list of their teammates
SELECT ?forename ?surname (GROUP_CONCAT(DISTINCT ?teammateName; SEPARATOR="; ") AS ?teammatesList)
WHERE {

# Make sure the drivers drove for the same team AT the same race.
# We want to avoid drivers who drove for the same team in two different periods:
# -Lauda and Schumacher both drove for Ferrari, but in different periods
# -Schumacher and Hamilton both drove in the same period (2010-12),
# but in that period they drove for different teams.
# They also drove for the same team, but never at the same time for the same team.

  ?result1 :results_driver ?driver ;
          :results_race ?race ; # same race
          :results_constructor ?constructor . # same constructor
  
  ?result2 :results_driver ?teammate ;
          :results_race ?race ; # same race
          :results_constructor ?constructor . # same constructor

  ?driver :drivers_forename ?forename ;
          :drivers_surname ?surname .
  
  ?teammate :drivers_forename ?tmForename ;
          :drivers_surname ?tmSurname .
  
  BIND(CONCAT(?forename, " ", ?surname) AS ?driverName)
  BIND(CONCAT(?tmForename, " ", ?tmSurname) AS ?teammateName)
  FILTER(?driver != ?teammate)
}
GROUP BY ?forename ?surname