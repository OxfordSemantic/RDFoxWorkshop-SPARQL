PREFIX : <http://www.oxfordsemantic.tech/f1demo/>

# Drivers with their win percentage, ordered by win percentage
SELECT ?forename ?surname ?raceCount ?raceWins ?percentage
WHERE {
# First get the drivers
?driver :drivers_forename ?forename ;
        :drivers_surname ?surname .

# Then get the race count for each driver with an innery query...
    {SELECT ?driver (COUNT(?race) AS ?raceCount)
    WHERE {
    ?result :results_driver ?driver ;
            :results_race ?race .
    }
    GROUP BY ?driver}

# ... and get the *win* count for each driver with another inner query.
    {SELECT ?driver (COUNT(?race) AS ?raceWins)
    WHERE {
    ?result :results_driver ?driver ;
            :results_race ?race ;
            :results_positionOrder 1 .
    }
    GROUP BY ?driver}

# Finally use the two aggregate variables to compute a percentage
# with the BIND keyword.
BIND(?raceWins/?raceCount AS ?percentage)

}
ORDER BY DESC(?percentage)